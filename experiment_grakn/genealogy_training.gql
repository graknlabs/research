
define

example-id sub attribute, datatype long;

person sub entity,
    has example-id,
    plays parent,
    plays child,
    plays sibling,
    plays candidate-sibling;

parentship sub relation,
    has example-id,
    relates parent,
    relates child;

siblingship sub relation,
    has example-id,
    relates sibling;

candidate-siblingship sub relation,
    relates candidate-sibling;

add-candidate-siblingships sub rule,
when {
    $p1 isa person;
    $p2 isa person;
    $p1 != $p2;
    not{(sibling: $p1, sibling: $p2) isa siblingship;};
}, then {
    (candidate-sibling: $p1, candidate-sibling: $p2) isa candidate-siblingship;
};

when-people-share-parents-they-are-siblings sub rule,
when {
    $p1 isa person;
    $p2 isa person;
    $p3 isa person;
    (parent: $p1, child: $p2) isa parentship;
    (parent: $p1, child: $p3) isa parentship;
    $p2 != $p3;
}, then {
    (sibling: $p2, sibling: $p3) isa siblingship;
};

when-people-share-siblings-they-are-siblings sub rule,
when {
    $p1 isa person;
    $p2 isa person;
    $p3 isa person;
    (sibling: $p1, sibling: $p2) isa siblingship;
    (sibling: $p2, sibling: $p3) isa siblingship;
    $p1 != $p2;
    $p2 != $p3;
    $p1 != $p3;
}, then {
    (sibling: $p1, sibling: $p3) isa siblingship;
};


# Data

# Case 1
insert
$p1 isa person, has example-id 0;
$p2 isa person, has example-id 0;
$p3 isa person, has example-id 0;
(parent: $p1, child: $p2) isa parentship, has example-id 0;
(parent: $p1, child: $p3) isa parentship, has example-id 0;
